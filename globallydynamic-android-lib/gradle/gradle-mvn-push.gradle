apply plugin: 'signing'

group = GROUP
version = VERSION_NAME

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL')
            ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL')
            ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
    return hasProperty('SONATYPE_NEXUS_USERNAME') ? SONATYPE_NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('SONATYPE_NEXUS_PASSWORD') ? SONATYPE_NEXUS_PASSWORD : ""
}

afterEvaluate { project ->
    if (project.getPlugins().hasPlugin('com.android.application') ||
            project.getPlugins().hasPlugin('com.android.library')) {

        task androidSourcesJar(type: Jar) {
            archiveClassifier = 'sources'
            from android.sourceSets.main.java.source
        }

        apply plugin: 'maven-publish'

        publishing {
            publications {
                android.libraryVariants.findAll { it.baseName.contains("release") }.collect { variant ->
                    "${variant.name}Aar"(MavenPublication) {
                        version = version
                        groupId = GROUP

                        from components.findByName(variant.name)
                        artifact project.androidSourcesJar

                        pom {
                            name = POM_NAME
                            description = POM_DESCRIPTION
                            url = POM_URL

                            scm {
                                url = POM_SCM_URL
                                connection = POM_SCM_CONNECTION
                                developerConnection = POM_SCM_DEV_CONNECTION
                            }

                            licenses {
                                license {
                                    name = POM_LICENCE_NAME
                                    url = POM_LICENCE_URL
                                    distribution = POM_LICENCE_DIST
                                }
                            }

                            developers {
                                developer {
                                    id = POM_DEVELOPER_ID
                                    name = POM_DEVELOPER_NAME
                                }
                            }
                        }

                        if (project.hasProperty("mainPublication")
                                && variant.baseName == project.getProperty("mainPublication")
                                || variant.baseName == "release") {
                            alias false
                            artifactId = POM_ARTIFACT_ID

                            if (project.hasProperty("mainPublicationDependencies")) {
                                def mainPublicationDeps = project.getProperty("mainPublicationDependencies")
                                pom.withXml {
                                    asNode().children().find { node ->
                                        node.name().localPart == "dependencies"
                                    }.children().each { node ->
                                        def groupId = node.get("groupId").first().value().join()
                                        if (groupId == GROUP) {
                                            def artifact = node.get("artifactId").first()
                                            def artifactValue = artifact.value().join().trim()
                                            def artifactProject = rootProject.subprojects.find { subproject ->
                                                subproject.hasProperty("POM_ARTIFACT_ID") && subproject.POM_ARTIFACT_ID == artifactValue
                                            }

                                            if (artifactProject != null
                                                    && mainPublicationDeps.containsKey(artifactValue)
                                                    && (artifactProject.getPlugins().hasPlugin('com.android.application')
                                                    || artifactProject.getPlugins().hasPlugin('com.android.library'))) {
                                                artifact.setValue([mainPublicationDeps[artifactValue]])
                                            }

                                        }
                                    }
                                }
                            }
                        } else if (variant.baseName.endsWith("-release")) {
                            alias true
                            artifactId = "$POM_ARTIFACT_ID-${variant.flavorName}"

                            pom.withXml {
                                asNode().children().find { node ->
                                    node.name().localPart == "dependencies"
                                }.children().each { node ->
                                    def groupId = node.get("groupId").first().value().join()
                                    if (groupId == GROUP) {
                                        def artifact = node.get("artifactId").first()
                                        def artifactValue = artifact.value().join().trim()
                                        def artifactProject = rootProject.subprojects.find { subproject ->
                                            subproject.hasProperty("POM_ARTIFACT_ID") && subproject.POM_ARTIFACT_ID == artifactValue
                                        }

                                        if (artifactProject != null
                                                && (artifactProject.getPlugins().hasPlugin('com.android.application')
                                                || artifactProject.getPlugins().hasPlugin('com.android.library'))) {
                                            def hasMatchingFlavor = artifactProject.extensions.findByName('android').libraryVariants.any {
                                                it.flavorName == variant.flavorName
                                            }
                                            if (hasMatchingFlavor) {
                                                artifact.setValue(["$artifactValue-${variant.flavorName}"])
                                            }
                                        }
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }

        if (isReleaseBuild()) {
            signing {
                required { isReleaseBuild() && gradle.taskGraph.hasTask("publish") }
                sign publishing.publications.findAll { it.name.endsWith("Aar") } as Publication[]
            }
        }
    } else {
        apply plugin: 'maven-publish'

        task sourcesJar(type: Jar, dependsOn: classes) {
            archiveClassifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            archiveClassifier = 'javadoc'
            from javadoc.destinationDir
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    artifact sourcesJar
                    artifact javadocJar
                    from components.java

                    groupId = GROUP
                    artifactId = POM_ARTIFACT_ID
                    version = version

                    pom {
                        name = POM_NAME
                        packaging = POM_PACKAGING
                        description = POM_DESCRIPTION
                        url = POM_URL

                        scm {
                            url = POM_SCM_URL
                            connection = POM_SCM_CONNECTION
                            developerConnection = POM_SCM_DEV_CONNECTION
                        }

                        licenses {
                            license {
                                name = POM_LICENCE_NAME
                                url = POM_LICENCE_URL
                                distribution = POM_LICENCE_DIST
                            }
                        }

                        developers {
                            developer {
                                id = POM_DEVELOPER_ID
                                name = POM_DEVELOPER_NAME
                            }
                        }
                    }
                }
            }
        }

        signing {
            required { isReleaseBuild() && gradle.taskGraph.hasTask("publish") }
            sign publishing.publications.mavenJava
        }
    }

    publishing {
        repositories {
            maven {
                url isReleaseBuild() ? getReleaseRepositoryUrl() : getSnapshotRepositoryUrl()
                credentials {
                    username = getRepositoryUsername()
                    password = getRepositoryPassword()
                }
            }
        }
    }
}