import org.jetbrains.kotlin.gradle.dsl.JvmTarget

buildscript {
    apply from: 'deps.gradle'

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    }
}

ext {
    sourceCompatibilityVersion = JavaVersion.VERSION_21
    targetCompatibilityVersion = JavaVersion.VERSION_21
    jvmTargetVersion = JvmTarget.JVM_21.target
}

subprojects {
    afterEvaluate { project ->
        tasks.withType(Test) {
            testLogging {
                exceptionFormat "full"
                showCauses true
                showExceptions true
                showStackTraces true
                showStandardStreams true
                events = ["passed", "skipped", "failed", "standardOut", "standardError"]
            }
        }

        repositories {
            google()
            mavenCentral()
        }

        if (project.hasProperty('java')) {
            sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
            targetCompatibility = rootProject.ext.targetCompatibilityVersion

            compileKotlin {
                kotlinOptions {
                    jvmTarget = rootProject.ext.jvmTargetVersion
                }
            }
            compileTestKotlin {
                kotlinOptions {
                    jvmTarget = rootProject.ext.jvmTargetVersion
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
